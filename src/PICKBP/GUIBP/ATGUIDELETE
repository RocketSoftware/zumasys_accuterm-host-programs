SUBROUTINE ATGUIDELETE(APPID, FORMID, CTLID, ERRORS, STATE)
**************************************************************************
*Copyright (c) 1999-2007 Zumasys,Inc. as an unpublished work. All rights *
*reserved. This work is the property of and embodies trade secrets and   *
*confidential information proprietary to Zumasys, Inc.  It may not be    *
*reproduced, copied, used, disclosed, transferred, adapted or modified   *
*without the express written approval of Zumasys, Inc., except as        *
*provided for in the accompanying warranty notice and licensing          *
*agreement.                                                              *
**************************************************************************
*
* MODULE: ATGUIDELETE
* AUTHOR: PJS
* VERSION: 5.3.2
* CREATED: 11/01/1999
* UPDATED: 04/10/2007
*
* MAINTENANCE RECORD:
*
* 5.3.2
*  PJS 04/10/2007
*   Added call to new ATGUICLEAREVENTS routine to delete pending
*   events sourced from deleted object.
*
* 5.3.1
*  PJS 06/12/2006
*   Updated to use new version of FTOBJREQ, which requires that
*   the PARAMS and CLIENV arrays be completely initialized.
*
* 4.0.1
*  PJS 09/25/2000
*   CLEANUP FOR ACCUTERM 2000 RELEASE
*
**************************************************************************
**************************************************************************
*
* ACCUTERM GUI MANAGER - DELETE ITEM
*
* CALL THIS SUBROUTINE TO DELETE GUI ITEMS.
*
* INPUT: ITEMS
*  APPID      application identifier (use same ID as when app created)
*  FORMID     identifier of form being deleted
*  CTLID      identifier of control being deleted
*
* OUTPUT:
*  ERRORS     ERRORS<1> is non-zero if errors have occurred
*
* OTHER:
*  STATE      internal use only - do not modify
*
**************************************************************************
*
$INCLUDE ATGUIEQUATES
EQU THISPGM TO 'ATGUIDELETE'
EQU AM TO CHAR(254), VM TO CHAR(253), SVM TO CHAR(252)
DIM PARAMS(30), CLIENV(10)
ERRORS = GRSUCCESS
FOR I = 1 TO 30
 PARAMS(I) = STATE<20,I>
NEXT I
FOR I = 1 TO 10
 CLIENV(I) = STATE<20+I>
NEXT I
IF NOT(STATE<1>) THEN
 CALL ATGUIERROR(3, THISPGM, '', 0, GRFATAL, '', ERRORS)
 RETURN
END
ID = APPID
IF FORMID NE '' THEN
 ID = ID : '*' : FORMID
 IF CTLID NE '' THEN
  ID = ID : '*' : CTLID
 END
END ELSE
 IF CTLID NE '' THEN
  ID = ID : '**' : CTLID
 END
END
REC = GCDELETE : VM : ID
*
* CHECK IF RECORDING MACRO
*
IF STATE<2> THEN
 STATE = STATE : AM : REC ;* APPEND TO MACRO
 RETURN
END
*
* SEND COMMAND TO SERVER
*
CALL FTOBJREQ('g', REC, RSP, 0, MAT PARAMS, MAT CLIENV)
CALL ATGUIERRORS(RSP, ERRORS)
*
* DISCARD PENDING EVENTS THAT WERE SOURCED FROM DELETED OBJECT
IF ERRORS<1> = 0 THEN
 CALL ATGUICLEAREVENTS(APPID, FORMID, CTLID, 0, 1, ERRORS, STATE)
END
RETURN
*
END
