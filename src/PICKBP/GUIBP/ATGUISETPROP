SUBROUTINE ATGUISETPROP(APPID, FORMID, CTLID, PROPERTY, COL, ROW, VALUE, ERRORS, STATE)
**************************************************************************
*Copyright (c) 1999-2006 Zumasys,Inc. as an unpublished work. All rights *
*reserved. This work is the property of and embodies trade secrets and   *
*confidential information proprietary to Zumasys, Inc.  It may not be    *
*reproduced, copied, used, disclosed, transferred, adapted or modified   *
*without the express written approval of Zumasys, Inc., except as        *
*provided for in the accompanying warranty notice and licensing          *
*agreement.                                                              *
**************************************************************************
*
* MODULE: ATGUISETPROP
* AUTHOR: PJS
* VERSION: 5.3.1
* CREATED: 11/01/1999
* UPDATED: 06/12/2006
*
* MAINTENANCE RECORD:
*
* 5.3.1
*  PJS 06/12/2006
*   Updated to use new version of FTOBJREQ, which requires that
*   the PARAMS and CLIENV arrays be completely initialized.
*
* 4.0.2
*  PJS 11/26/2003
*   CORRECTED ID LOGIC WHEN APPID & CTRLID BUT NO FORMID (MENUS ON MDI APP)
*
* 4.0.1
*  PJS 09/25/2000
*   CLEANUP FOR ACCUTERM 2000 RELEASE
*
**************************************************************************
**************************************************************************
*
* ACCUTERM GUI MANAGER - SET ITEM PROPERTY
*
* CALL THIS SUBROUTINE TO SET THE PROPERTY VALUE OF A GUI ITEM.
*
* INPUT:
*  APPID      application identifier (use same ID when deleting app)
*  FORMID     form identifier of form being created
*  CTLID      control identifier (unique within scope of form)
*  PROPERTY   property code for property to be set
*  COL        column; 0 if property is a scalar or all columns
*  ROW        row; 0 if property is a vector or all rows
*  VALUE      value to set property; if property is an array or
*               vector, separate rows with value marks and columns
*               with subvalue marks
*
* OUTPUT:
*  ERRORS     ERRORS<1> is non-zero if errors have occurred
*
* OTHER:
*  STATE      internal use only - do not modify
*
**************************************************************************
*
$INCLUDE ATGUIEQUATES
EQU THISPGM TO 'ATGUISETPROP'
EQU AM TO CHAR(254), VM TO CHAR(253), SVM TO CHAR(252)
DIM PARAMS(30), CLIENV(10)
ERRORS = GRSUCCESS
FOR I = 1 TO 30
 PARAMS(I) = STATE<20,I>
NEXT I
FOR I = 1 TO 10
 CLIENV(I) = STATE<20+I>
NEXT I
IF NOT(STATE<1>) THEN
 CALL ATGUIERROR(3, THISPGM, '', 0, GRFATAL, '', ERRORS)
 RETURN
END
ID = APPID
IF FORMID NE '' THEN
 ID = ID : '*' : FORMID
 IF CTLID NE '' THEN
  ID = ID : '*' : CTLID
 END
END ELSE
 IF CTLID NE '' THEN
  ID = ID : '**' : CTLID
 END
END
REC = GCSETPROP : VM : ID : VM : PROPERTY : VM : COL : VM : ROW : VM : VALUE<1>
*
* IF PROPERTY IS SCALE, SAVE CURRENT VALUE
*
IF PROPERTY EQ GPSCALE THEN STATE<3> = VALUE<1>
*
* CHECK IF RECORDING MACRO
*
IF STATE<2> THEN
 STATE = STATE : AM : REC ;* APPEND TO MACRO
 RETURN
END
*
* SEND COMMAND TO SERVER
*
CALL FTOBJREQ('g', REC, RSP, 0, MAT PARAMS, MAT CLIENV)
CALL ATGUIERRORS(RSP, ERRORS)
RETURN
*
END
