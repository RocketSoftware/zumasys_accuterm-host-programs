SUBROUTINE ATGUICREATEBROWSER(APPID, FORMID, CTLID, PARENTID, EVENTMASK, URL, Left, TOP, WIDTH, HEIGHT, ERRORS, STATE)
**************************************************************************
*Copyright (c) 2009-2010 Zumasys,Inc. as an unpublished work. All rights *
*reserved. This work is the property of and embodies trade secrets and   *
*confidential information proprietary to Zumasys, Inc.  It may not be    *
*reproduced, copied, used, disclosed, transferred, adapted or modified   *
*without the express written approval of Zumasys, Inc., except as        *
*provided for in the accompanying warranty notice and licensing          *
*agreement.                                                              *
**************************************************************************
*
* MODULE: ATGUICREATEBROWSER
* AUTHOR: PJS
* VERSION: 7.0.2
* CREATED: 09/15/2009
* UPDATED: 06/24/2010
*
* MAINTENANCE RECORD:
*
* 7.0.2
*  PJS 06/24/2010
*   Fixed unassigned variable compile error (CAPTION s/b URL).
*
* 7.0.1
*  PJS 09/15/2009
*   Created
*
**************************************************************************
**************************************************************************
*
* ACCUTERM GUI MANAGER - CREATE BROWSER CONTROL
*
* CALL THIS SUBROUTINE TO CREATE A BROWSER CONTROL.
*
* INPUT:
*
*  APPID      application identifier
*  FORMID     form identifier 
*  CTLID      control identifier (unique within scope of form)
*  PARENTID   identifier of parent container (eg frame) if any
*  EVENTMASK  all forms must specify GECLOSE
*  URL        initial URL (null is OK)
*  Left       horizontal position of the control relative to its
*               parent form or frame
*  TOP        vertical position of the control relative to its
*               parent form or frame
*  WIDTH      width of the control
*  HEIGHT     height of the control
*
* OUTPUT:
*  ERRORS     ERRORS<1> is non-zero if errors have occurred
*
* OTHER:
*  STATE      internal use only - do not modify
*
**************************************************************************
*
$INCLUDE ATGUIEQUATES
EQU THISPGM TO 'ATGUICREATEBROWSER'
EQU AM TO CHAR(254), VM TO CHAR(253), SVM TO CHAR(252)
DIM PARAMS(30), CLIENV(10)
ERRORS = GRSUCCESS
FOR I = 1 TO 30
 PARAMS(I) = STATE<20,I>
NEXT I
FOR I = 1 TO 10
 CLIENV(I) = STATE<20+I>
NEXT I
IF NOT(STATE<1>) THEN
 CALL ATGUIERROR(3, THISPGM, '', 0, GRFATAL, '', ERRORS)
 RETURN
END
*
* VALIDATE PARAMETERS
*
IF APPID EQ '' OR FORMID EQ '' OR CTLID EQ '' THEN
 CALL ATGUIERROR(2, THISPGM, FORMID, '', GRNOID, '', ERRORS)
 RETURN
END
ID = APPID : '*' : FORMID : '*' : CTLID
REC = GCCREATE : VM : ID : VM : PARENTID : VM : GXBROWSER : VM : Left : VM : TOP : VM : WIDTH : VM : HEIGHT : VM : EVENTMASK
IF URL NE '' THEN
 REC = REC : AM : GCSETPROP : VM : VM : GPDEFVAL : VM :VM : VM : URL
END
*
* CHECK IF RECORDING MACRO
*
IF STATE<2> THEN
 STATE = STATE : AM : REC ;* APPEND TO MACRO
 RETURN
END
*
* SEND COMMAND TO SERVER
*
CALL FTOBJREQ('g', REC, RSP, 0, MAT PARAMS, MAT CLIENV)
CALL ATGUIERRORS(RSP, ERRORS)
RETURN
*
END
