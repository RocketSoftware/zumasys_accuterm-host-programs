SUBROUTINE ATGUICREATETREE(APPID, FORMID, CTLID, PARENTID, EVENTMASK, STYLE, Left, TOP, WIDTH, HEIGHT, ERRORS, STATE)
**************************************************************************
*Copyright (c) 1999-2006 Zumasys,Inc. as an unpublished work. All rights *
*reserved. This work is the property of and embodies trade secrets and   *
*confidential information proprietary to Zumasys, Inc.  It may not be    *
*reproduced, copied, used, disclosed, transferred, adapted or modified   *
*without the express written approval of Zumasys, Inc., except as        *
*provided for in the accompanying warranty notice and licensing          *
*agreement.                                                              *
**************************************************************************
*
* MODULE: ATGUICREATELIST
* AUTHOR: PJS
* VERSION: 5.3.1
* CREATED: 05/28/2001
* UPDATED: 06/12/2006
*
* MAINTENANCE RECORD:
*
* 5.3.1
*  PJS 06/12/2006
*   Updated to use new version of FTOBJREQ, which requires that
*   the PARAMS and CLIENV arrays be completely initialized.
*
* 4.0.1
*  PJS 05/28/2001
*   Created
*
**************************************************************************
**************************************************************************
*
* ACCUTERM GUI MANAGER - CREATE TREE CONTROL
*
* CALL THIS SUBROUTINE TO CREATE A TREE CONTROL.
*
* INPUT:
*  APPID      application identifier
*  FORMID     form identifier
*  CTLID      control identifier (unique within scope of form)
*  PARENTID   identifier of parent container (eg frame) if any
*  EVENTMASK  
*  STYLE      0 = text only, 1 = text & sign, 2 = text & small icon
*               3 = text & small icon & sign, 4 = text & large icon
*               5 = text & large icon & sign
*  Left       horizontal position of the control relative to its
*               parent form or frame
*  TOP        vertical position of the control relative to its
*               parent form or frame
*  WIDTH      width of the control
*  HEIGHT     height of the control
*
* OUTPUT:
*  ERRORS     ERRORS<1> is non-zero if errors have occurred
*
* OTHER:
*  STATE      internal use only - do not modify
*
**************************************************************************
*
$INCLUDE ATGUIEQUATES
EQU THISPGM TO 'ATGUICREATETREE'
EQU AM TO CHAR(254), VM TO CHAR(253), SVM TO CHAR(252)
DIM PARAMS(30), CLIENV(10)
ERRORS = GRSUCCESS
FOR I = 1 TO 30
 PARAMS(I) = STATE<20,I>
NEXT I
FOR I = 1 TO 10
 CLIENV(I) = STATE<20+I>
NEXT I
IF NOT(STATE<1>) THEN
 CALL ATGUIERROR(3, THISPGM, '', 0, GRFATAL, '', ERRORS)
 RETURN
END
*
* VALIDATE PARAMETERS
*
IF APPID EQ '' OR FORMID EQ '' OR CTLID EQ '' THEN
 CALL ATGUIERROR(2, THISPGM, FORMID, '', GRNOID, '', ERRORS)
 RETURN
END
IF STYLE < 0 OR STYLE > 5 THEN
 CALL ATGUIERROR(2, THISPGM, CTLID, 'STYLE', GRINVARG, '', ERRORS)
 RETURN
END
ID = APPID : '*' : FORMID : '*' : CTLID
REC = GCCREATE : VM : ID : VM : PARENTID : VM : GXTREE : VM : Left : VM : TOP : VM : WIDTH : VM : HEIGHT : VM : EVENTMASK
IF STYLE NE 0 THEN
 REC = REC : AM : GCSETPROP : VM : VM : GPSTYLE : VM : VM : VM : STYLE
END
*
* CHECK IF RECORDING MACRO
*
IF STATE<2> THEN
 STATE = STATE : AM : REC ;* APPEND TO MACRO
 RETURN
END
*
* SEND COMMAND TO SERVER
*
CALL FTOBJREQ('g', REC, RSP, 0, MAT PARAMS, MAT CLIENV)
CALL ATGUIERRORS(RSP, ERRORS)
RETURN
*
END
