SUBROUTINE ATGUIGETITEMPROP(APPID, FORMID, CTLID, ITMID, PROPERTY, COL, ROW, VALUE, ERRORS, STATE)
**************************************************************************
*Copyright (c) 2006 Zumasys,Inc. as an unpublished work. All rights      *
*reserved. This work is the property of and embodies trade secrets and   *
*confidential information proprietary to Zumasys, Inc.  It may not be    *
*reproduced, copied, used, disclosed, transferred, adapted or modified   *
*without the express written approval of Zumasys, Inc., except as        *
*provided for in the accompanying warranty notice and licensing          *
*agreement.                                                              *
**************************************************************************
*
* MODULE: ATGUIGETITEMPROP
* AUTHOR: PJS
* VERSION: 5.3.1
* CREATED: 12/23/2004
* UPDATED: 06/12/2006
*
* MAINTENANCE RECORD:
*
* 5.3.1
*  PJS 06/12/2006
*   Updated to use new version of FTOBJREQ, which requires that
*   the PARAMS and CLIENV arrays be completely initialized.
*
* 5.2.1
*  PJS 12/23/2004
*   CREATED
*
**************************************************************************
**************************************************************************
*
* ACCUTERM GUI MANAGER - GET ITEM PROPERTY
*
* CALL THIS SUBROUTINE TO GET THE PROPERTY VALUE OF A GUI ITEM.
*
* INPUT:
*
*  APPID      application identifier
*  FORMID     form identifier
*  CTLID      control identifier
*  ITMID      ID of menu or tree item desired
*  PROPERTY   property code for property to be retrieved
*  COL        column; 0 if property is a scalar or all columns
*  ROW        row; 0 if property is a vector or all rows
*
* OUTPUT:
*  VALUE      value of retrieved property; if property is an array or
*               vector, rows are separated by value marks and columns
*               by subvalue marks
*  ERRORS     ERRORS<1> is non-zero if errors have occurred
*
* OTHER:
*  STATE      internal use only - do not modify
*
**************************************************************************
*
$INCLUDE ATGUIEQUATES
EQU THISPGM TO 'ATGUIGETITEMPROP'
EQU AM TO CHAR(254), VM TO CHAR(253), SVM TO CHAR(252)
DIM PARAMS(30), CLIENV(10)
ERRORS = GRSUCCESS
FOR I = 1 TO 30
 PARAMS(I) = STATE<20,I>
NEXT I
FOR I = 1 TO 10
 CLIENV(I) = STATE<20+I>
NEXT I
IF NOT(STATE<1>) THEN
 CALL ATGUIERROR(3, THISPGM, '', 0, GRFATAL, '', ERRORS)
 RETURN
END
ID = APPID
IF FORMID NE '' THEN
 ID = ID : '*' : FORMID
 IF CTLID NE '' THEN
  ID = ID : '*' : CTLID
  IF ITMID NE '' THEN
   ID = ID : '*' : ITMID
  END
 END ELSE
  IF ITMID NE '' THEN
   ID = ID : '**' : ITMID
  END
 END
END ELSE
 IF CTLID NE '' THEN
  ID = ID : '**' : CTLID
  IF ITMID NE '' THEN
   ID = ID : '*' : ITMID
  END
 END ELSE
  IF ITMID NE '' THEN
   ID = ID : '***' : ITMID
  END
 END
END
REC = GCGETPROP : VM : ID : VM : PROPERTY : VM : COL : VM : ROW
*
* CHECK IF RECORDING MACRO
*
IF STATE<2> THEN
 CALL ATGUIERROR(2, THISPGM, ID, 0, GRNOMAC, '', ERRORS)
 RETURN
END
*
* SEND COMMAND TO SERVER
*
CALL FTOBJREQ('g', REC, RSP, 0, MAT PARAMS, MAT CLIENV)
CALL ATGUIERRORS(RSP, ERRORS)
VALUE = RSP
RETURN
*
END
