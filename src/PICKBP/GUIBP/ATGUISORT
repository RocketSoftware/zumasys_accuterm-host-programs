SUBROUTINE ATGUISORT(APPID, FORMID, CTLID, COLS, ERRORS, STATE)
**************************************************************************
*Copyright (c) 2009 Zumasys,Inc. as an unpublished work. All rights      *
*reserved. This work is the property of and embodies trade secrets and   *
*confidential information proprietary to Zumasys, Inc.  It may not be    *
*reproduced, copied, used, disclosed, transferred, adapted or modified   *
*without the express written approval of Zumasys, Inc., except as        *
*provided for in the accompanying warranty notice and licensing          *
*agreement.                                                              *
**************************************************************************
*
* MODULE: ATGUISORT
* AUTHOR: PJS
* VERSION: 7.0.1
* CREATED: 08/31/2009
*
* MAINTENANCE RECORD:
*
**************************************************************************
**************************************************************************
*
* ACCUTERM GUI MANAGER - SORT ITEMS
*
* CALL THIS SUBROUTINE TO SORT ITEMS IN A LIST, COMBOBOX OR GRID
*
* INPUT:
*  APPID      application identifier
*  FORMID     form identifier
*  CTLID      control identifier of object containing items to sort
*  COLS       list of columns to sort on, separated by VM
*             append optional column sort order 'A' or 'D' after
*             the column number, separated by SVM.
*
* OUTPUT:
*  ERRORS     ERRORS<1> is non-zero if errors have occurred
*
* OTHER:
*  STATE      internal use only - do not modify
*
* Notes:
*
*  - the sort order for each control type (list, combo, grid) may not
*    be identical. For example, if the sort key(s) for two rows are
*    essentially the same, the order of those rows is not defined.
*  - the ColDataType property can be used to specify the data type
*    (and indirectly the sort type) for columns in a list, combo or
*    grid. This is necessary if you wish to sort on date or time data.
*  - if ColDataType is 'any', then the column alignment is used to
*    determine the sort type - right-aligned columns are assumed to
*    contain numeric data and will be sorted based on that assumption.
*    If a string is encountered it is treated numerically as zero.
*    Digits that precede a string are treated as numeric and the trailing
*    string is ignored.
*
**************************************************************************
*
$INCLUDE ATGUIEQUATES
EQU THISPGM TO 'ATGUISORT'
EQU AM TO CHAR(254), VM TO CHAR(253), SVM TO CHAR(252)
DIM PARAMS(30), CLIENV(10)
ERRORS = GRSUCCESS
FOR I = 1 TO 30
 PARAMS(I) = STATE<20,I>
NEXT I
FOR I = 1 TO 10
 CLIENV(I) = STATE<20+I>
NEXT I
IF NOT(STATE<1>) THEN
 CALL ATGUIERROR(3, THISPGM, '', 0, GRFATAL, '', ERRORS)
 RETURN
END
IF APPID EQ '' OR FORMID EQ '' OR CTLID EQ '' THEN
 CALL ATGUIERROR(2, THISPGM, FORMID, '', GRNOID, '', ERRORS)
 RETURN
END
ID = APPID : '*' : FORMID : '*' : CTLID
REC = GCMETHOD : VM : ID : VM : GMSORT : VM : COLS
*
* CHECK IF RECORDING MACRO
*
IF STATE<2> THEN
 STATE = STATE : AM : REC ;* APPEND TO MACRO
 RETURN
END
*
* SEND COMMAND TO SERVER
*
CALL FTOBJREQ('g', REC, RSP, 0, MAT PARAMS, MAT CLIENV)
CALL ATGUIERRORS(RSP, ERRORS)
IF RSP = MBANSCANCEL THEN
 * USER CANCELLED ACTION
 CALL ATGUIERROR(1, THISPGM, '', 0, GRCANCEL, '', ERRORS)
END
RETURN
*
END
