SUBROUTINE ATGUIMSGBOX(PRMPT, CAPTION, STYLE, BUTTONS, HELPID, RESPONSE, ERRORS, STATE)
**************************************************************************
*Copyright (c) 1999-2006 Zumasys,Inc. as an unpublished work. All rights *
*reserved. This work is the property of and embodies trade secrets and   *
*confidential information proprietary to Zumasys, Inc.  It may not be    *
*reproduced, copied, used, disclosed, transferred, adapted or modified   *
*without the express written approval of Zumasys, Inc., except as        *
*provided for in the accompanying warranty notice and licensing          *
*agreement.                                                              *
**************************************************************************
*
* MODULE: ATGUIMSGBOX
* AUTHOR: PJS
* VERSION: 5.3.1
* CREATED: 11/01/1999
* UPDATED: 06/12/2006
*
* MAINTENANCE RECORD:
*
* 5.3.1
*  PJS 06/12/2006
*   Updated to use new version of FTOBJREQ, which requires that
*   the PARAMS and CLIENV arrays be completely initialized.
*
* 5.2.2
*  PJS 03/09/2005
*   Eliminate runtime error when HelpID is non-numeric
*
* 5.2.1
*  PJS 10/27/2004
*   Added support for "default" button: add 100, 200 OR 300
*   to the BUTTONS argument to designate the first, second
*   or third button as the default button.
*
* 4.0.1
*  PJS 09/25/2000
*   CLEANUP FOR ACCUTERM 2000 RELEASE
*
**************************************************************************
**************************************************************************
*
* ACCUTERM GUI MANAGER - DISPLAY MESSAGE BOX
*
* CALL THIS SUBROUTINE TO DISPLAY A MESSAGE BOX.
*
* INPUT:
*  PRMPT      prompt message - for multi-line message, separate lines
*               with SVM
*  CAPTION    caption text
*  STYLE      selects the icon displayed in the message box:
*               0 = no icon, 1 = "X", 2 = "!", 3 = "?", 4 = "i"
*  BUTTONS    selects which buttons are included in message box:
*               0 = OK, 1 = OK/Cancel, 2 = Abort/Retry/Ignore
*               3 = Yes/No/Cancel, 4 = Yes/No, 5 = Retry/Cancel
*               add 100, 200 or 300 to designate that the first,
*               second or third button is the default button
*  HELPID     help topic number to display when user presses F1 key
*               (optional, only supported when using WinHelp)
*
* OUTPUT:
*  RESPONSE   indicates which button was clicked: 1 = OK, 2 = Cancel,
*               3 = Abort, 4 = Retry, 5 = Ignore, 6 = Yes, 7 = No
*  ERRORS     ERRORS<1> is non-zero if errors have occurred
*
* OTHER:
*  STATE      internal use only - do not modify
*
*
**************************************************************************
*
$INCLUDE ATGUIEQUATES
EQU THISPGM TO 'ATGUIMSGBOX'
EQU AM TO CHAR(254), VM TO CHAR(253), SVM TO CHAR(252)
DIM PARAMS(30), CLIENV(10)
ERRORS = GRSUCCESS
FOR I = 1 TO 30
 PARAMS(I) = STATE<20,I>
NEXT I
FOR I = 1 TO 10
 CLIENV(I) = STATE<20+I>
NEXT I
IF NOT(STATE<1>) THEN
 CALL ATGUIERROR(3, THISPGM, '', 0, GRFATAL, '', ERRORS)
 RETURN
END
*
* VALIDATE PARAMETERS
*
BEGIN CASE
 CASE STYLE EQ 0
  XTYPE = 0
 CASE STYLE EQ 1
  XTYPE = 16
 CASE STYLE EQ 2
  XTYPE = 48
 CASE STYLE EQ 3
  XTYPE = 32
 CASE STYLE EQ 4
  XTYPE = 64
 CASE 1
  CALL ATGUIERROR(2, THISPGM, '', 'STYLE', GRINVARG, '', ERRORS)
  RETURN
END CASE
DEFBTN = INT(BUTTONS / 100)
BTNS = BUTTONS - (DEFBTN * 100)
IF BTNS >= 0 AND BTNS <= 5 AND DEFBTN >= 0 AND DEFBTN <= 3 THEN
 IF DEFBTN > 1 THEN DEFBTN = (DEFBTN - 1) * 256 ELSE DEFBTN = 0
 XTYPE = XTYPE + BTNS + DEFBTN
END ELSE
 CALL ATGUIERROR(2, THISPGM, '', 'BUTTONS', GRINVARG, '', ERRORS)
 RETURN
END
IF HELPID <> '' AND HELPID <> '0' THEN XTYPE = XTYPE + 16384
RESPONSE = 0
REC = GCMSGBOX : VM : PRMPT : VM : CAPTION : VM : XTYPE : VM : HELPID
*
* CHECK IF RECORDING MACRO
*
IF STATE<2> THEN
 CALL ATGUIERROR(2, THISPGM, '', 0, GRNOMAC, '', ERRORS)
 RETURN
END
CALL FTOBJREQ('g', REC, RSP, 0, MAT PARAMS, MAT CLIENV)
CALL ATGUIERRORS(RSP, ERRORS)
RESPONSE = RSP
RETURN
*
END
