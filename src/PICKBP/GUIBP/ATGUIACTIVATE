SUBROUTINE ATGUIACTIVATE(APPID, FORMID, CTLID, ERRORS, STATE)
**************************************************************************
*Copyright (c) 1999-2007 Zumasys,Inc. as an unpublished work. All rights *
*reserved. This work is the property of and embodies trade secrets and   *
*confidential information proprietary to Zumasys, Inc.  It may not be    *
*reproduced, copied, used, disclosed, transferred, adapted or modified   *
*without the express written approval of Zumasys, Inc., except as        *
*provided for in the accompanying warranty notice and licensing          *
*agreement.                                                              *
**************************************************************************
*
* MODULE: ATGUIACTIVATE
* AUTHOR: PJS
* VERSION: 5.3.3
* CREATED: 11/01/1999
* UPDATED: 04/10/2007
*
* MAINTENANCE RECORD:
*
* 5.3.3
*  PJS 04/10/2007
*   Call new ATGUICLEAREVENTS routine to remove selected events from
*   local event queue, instead of using similar code here.
*
* 5.3.2
*  PJS 06/12/2006
*   Updated to use new version of FTOBJREQ, which requires that
*   the PARAMS and CLIENV arrays be completely initialized.
*
* 5.3.1
*  PJS 03/28/2006
*   Include VALIDATECELL, ACTIVATECELL, DEACTIVATECELL, VALIDATEROW,
*   ACTIVATEROW, DEACTIVATEROW in list of events to discard from the
*   local event queue, for consistency.
*
* 5.2.1
*  PJS 10/27/2004
*   Updated for GUI version 1.2 - scan local event queue and discard
*   pending VALIDATE, DEACTIVATE or ACTIVATE events.
*
* 4.0.1
*  PJS 09/25/2000
*   CLEANUP FOR ACCUTERM 2000 RELEASE
*
**************************************************************************
**************************************************************************
*
* ACCUTERM GUI MANAGER - ACTIVATE ITEM
*
* CALL THIS SUBROUTINE TO ACTIVATE GUI ITEM.
*
* INPUT: ITEMS
*  APPID      application identifier (use same ID as when app created)
*  FORMID     identifier of form being deleted
*  CTLID      identifier of control being deleted
*
* OUTPUT:
*  ERRORS     ERRORS<1> is non-zero if errors have occurred
*
* OTHER:
*  STATE      internal use only - do not modify
*
**************************************************************************
*
$INCLUDE ATGUIEQUATES
EQU THISPGM TO 'ATGUIACTIVATE'
EQU AM TO CHAR(254), VM TO CHAR(253), SVM TO CHAR(252)
DIM PARAMS(30), CLIENV(10)
ERRORS = GRSUCCESS
FOR I = 1 TO 30
 PARAMS(I) = STATE<20,I>
NEXT I
FOR I = 1 TO 10
 CLIENV(I) = STATE<20+I>
NEXT I
IF NOT(STATE<1>) THEN
 CALL ATGUIERROR(3, THISPGM, '', 0, GRFATAL, '', ERRORS)
 RETURN
END
ID = APPID
IF FORMID NE '' THEN
 ID = ID : '*' : FORMID
 IF CTLID NE '' THEN
  ID = ID : '*' : CTLID
 END
END
REC = GCMETHOD : VM : ID : VM : GMACTIVATE
*
* CHECK IF RECORDING MACRO
*
IF STATE<2> THEN
 STATE = STATE : AM : REC ;* APPEND TO MACRO
 RETURN
END
*
* DISCARD PENDING VALIDATE, DEACTIVATE & ACTIVATE EVENTS FROM LOCAL EVENT QUEUE
*
EVTMSK = GEVALIDATE + GEDEACTIVATE + GEACTIVATE + GEVALIDATECELL + GEACTIVATECELL + GEDEACTIVATECELL + GEVALIDATEROW + GEACTIVATEROW + GEDEACTIVATEROW
CALL ATGUICLEAREVENTS('', '', '', EVTMSK, 8, ERRORS, STATE)
*
* SEND COMMAND TO SERVER
*
CALL FTOBJREQ('g', REC, RSP, 0, MAT PARAMS, MAT CLIENV)
CALL ATGUIERRORS(RSP, ERRORS)
RETURN
*
END
