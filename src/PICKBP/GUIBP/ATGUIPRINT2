SUBROUTINE ATGUIPRINT2(APPID, FORMID, MODE, ERRORS, STATE)
**************************************************************************
*Copyright (c) 1999-2006 Zumasys,Inc. as an unpublished work. All rights *
*reserved. This work is the property of and embodies trade secrets and   *
*confidential information proprietary to Zumasys, Inc.  It may not be    *
*reproduced, copied, used, disclosed, transferred, adapted or modified   *
*without the express written approval of Zumasys, Inc., except as        *
*provided for in the accompanying warranty notice and licensing          *
*agreement.                                                              *
**************************************************************************
*
* MODULE: ATGUIPRINT2
* AUTHOR: PJS
* VERSION: 5.3.2
* CREATED: 11/10/2004
* UPDATED: 07/03/2006
*
* MAINTENANCE RECORD:
*
* 5.3.2
*  PJS 07/03/2006
*   If user cancels the printer dialog, an error is now returned.
*
* 5.3.1
*  PJS 06/12/2006
*   Updated to use new version of FTOBJREQ, which requires that
*   the PARAMS and CLIENV arrays be completely initialized.
*
* 5.1.2
*  PJS 11/10/2004
*   New subroutine introduced with GUI version 1.2 includes a MODE
*   argument which selects whether the Printer or Print Setup dialog
*   is displayed and allows printing the form without displaying any
*   dialog. This routine replaces the deprecated ATGUIPRINT subroutine.
*
**************************************************************************
**************************************************************************
*
* ACCUTERM GUI MANAGER - PRINT FORM / PRINT SETUP
*
* CALL THIS SUBROUTINE TO PRINT A FORM OR DISPLAY THE PRINT SETUP DIALOG
*
* INPUT:
*  APPID      application identifier
*  FORMID     form identifier of form being printed
*  MODE       0 = show Printer dialog & print form if user does not cancel
*             1 = print form using current printer selection or default printer
*             2 = show the Print Setup dialog
*
* OUTPUT:
*  ERRORS     ERRORS<1> is non-zero if errors have occurred
*
* OTHER:
*  STATE      internal use only - do not modify
*
**************************************************************************
*
$INCLUDE ATGUIEQUATES
EQU THISPGM TO 'ATGUIPRINT2'
EQU AM TO CHAR(254), VM TO CHAR(253), SVM TO CHAR(252)
DIM PARAMS(30), CLIENV(10)
ERRORS = GRSUCCESS
FOR I = 1 TO 30
 PARAMS(I) = STATE<20,I>
NEXT I
FOR I = 1 TO 10
 CLIENV(I) = STATE<20+I>
NEXT I
IF NOT(STATE<1>) THEN
 CALL ATGUIERROR(3, THISPGM, '', 0, GRFATAL, '', ERRORS)
 RETURN
END
ID = APPID : '*' : FORMID
REC = GCMETHOD : VM : ID : VM : GMPRINT : VM : MODE
*
* CHECK IF RECORDING MACRO
*
IF STATE<2> THEN
 STATE = STATE : AM : REC ;* APPEND TO MACRO
 RETURN
END
*
* SEND COMMAND TO SERVER
*
CALL FTOBJREQ('g', REC, RSP, 0, MAT PARAMS, MAT CLIENV)
CALL ATGUIERRORS(RSP, ERRORS)
IF RSP = MBANSCANCEL THEN
 * USER CANCELLED ACTION
 CALL ATGUIERROR(1, THISPGM, '', 0, GRCANCEL, '', ERRORS)
END
RETURN
*
END
